plugins {
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.23.1-alpha-2'
}

dependencies {
    implementation 'org.fxmisc.richtext:richtextfx:0.10.5'
}

repositories {
    mavenCentral()
}

javafx {
    version = '15'
    modules = ['javafx.controls']
}

version = '5.4.3'

application {
    mainModule.set("org.example.richtextfx")
    mainClass.set("org.example.richtextfx.Launcher")
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    customImage {
        jdkAdditive = true
        jdkModules = ['java.management','java.instrument','jdk.management.agent','jdk.crypto.ec']
        // appModules = ['java.keywords.merged.module']
        appModules = ['org.example.richtextfx']
    }

    enableCds()

    launcher {
        name = 'java-keywords'
        noConsole = true
        jvmArgs = ['-Dcom.sun.management.jmxremote']
    }
    addExtraDependencies("javafx")

    jpackage {
        jvmArgs = ['-Dcom.sun.management.jmxremote', '-splash:$APPDIR/splash.png']
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        icon = "src/main/resources/java.$imgType"
        installerOptions += [
                '--vendor', 'Acme Corporation',
                '--file-associations', 'src/main/resources/associations.properties',
                '--app-version', version
        ]
        if (currentOs.windows) {
            imageOptions += ['--win-console']
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
        }
    }
}
tasks.jpackageImage.doLast {
    copy {
        from "src/main/resources"
        include "splash.png"
        into "build/jpackage/$project.name/app"
    }
}
