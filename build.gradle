plugins {
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.22.0'
}

dependencies {
    implementation 'org.fxmisc.richtext:richtextfx:0.10.5'
}

repositories {
    mavenCentral()
}

javafx {
    version = '14'
    modules = ['javafx.controls']
}

version = '5.4.3'

application {
    mainModule.set("org.example.richtextfx")
    mainClass.set("org.example.richtextfx.JavaKeywordsDemo")
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'java-keywords'
        noConsole = true
    }
    addExtraDependencies("javafx")

    jpackage {
        jvmArgs = ['-splash:$APPDIR/splash.png']
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        icon = "src/main/resources/java.$imgType"
        // imageOptions += ['--win-console']
        installerOptions += [
                '--vendor', 'Acme Corporation',
                '--file-associations', 'src/main/resources/associations.properties',
                '--app-version', version
        ]
        if (currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
        }
    }
/*
    customImage {
        appModules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
    }
*/
}
tasks.jpackageImage.doLast {
    copy {
        from "src/main/resources"
        include "splash.png"
        into "build/jpackage/$project.name/app"
    }
}
